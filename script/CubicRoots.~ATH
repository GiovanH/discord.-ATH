// Cubic Formula

PROCREATE LOOP 0;
~ATH(LOOP) {
	print ("Enter the coefficients of the cubic equation ax^3 + bx^2 + cx + d = 0\n");
	input A "a: ";
	input B "b: ";
	input C "c: ";
	input D "d: ";
	PROCREATE J A*(C*(18*B*D - 4*C*C) - 27*A*D*D) + B*B*(C*C - 4*B*D);
	PROCREATE K B*B - 3*A*C;
	DEBATE(J == 0){
		DEBATE(K == 0){
			PROCREATE X1 -B / (3*A);
			PROCREATE X2 X1;
			PROCREATE X3 X1;
		}
		UNLESS{
			PROCREATE X1 (B*(4*A*C - B*B) - 9*A*A*D) / (A*K);
			PROCREATE X2 (9*A*D - B*C) / (2*K);
			PROCREATE X3 X2;
		}
	}
	UNLESS{
		PROCREATE R -0.5 + (0.75 ^ 0.5)*1j;
		PROCREATE L 2*B*B*B + 9*A*(3*A*D - B*C);
		PROCREATE M (-27*A*A*J) ^ 0.5;
		DEBATE(L == M){
			PROCREATE C1 ((L + M) / 2) ^ (1/3);
		}
		UNLESS{
			PROCREATE C1 ((L - M) / 2) ^ (1/3);
		}
		PROCREATE C2 C1 * R;
		PROCREATE C3 C1 / R;
		PROCREATE X1 -(B + C1 + (K/C1)) / (3*A);
		PROCREATE X2 -(B + C2 + (K/C2)) / (3*A);
		PROCREATE X3 -(B + C3 + (K/C3)) / (3*A);
	}
	print("The three roots are ~.4f, ~.4f, ~.4f\n", X1, X2, X3);
	LOOP.DIE();
} EXECUTE(NULL);
THIS.DIE();
